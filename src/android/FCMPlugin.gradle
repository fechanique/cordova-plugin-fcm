def FCM_VERSION = System.getenv("FCM_VERSION") ?: rootProject.hasProperty("FCM_VERSION") ? rootProject.ext.FCM_VERSION : '19.0.0'

buildscript {
    ext {
        GRADLE_TOOLS_VERSION = System.getenv("GRADLE_TOOLS_VERSION") ?: rootProject.hasProperty("GRADLE_TOOLS_VERSION") ? rootProject.ext.GRADLE_TOOLS_VERSION : '3.5.3'
        GOOGLE_SERVICES_VERSION = System.getenv("GOOGLE_SERVICES_VERSION") ?: rootProject.hasProperty("GOOGLE_SERVICES_VERSION") ? rootProject.ext.GOOGLE_SERVICES_VERSION : '4.3.3'

        ext.gradleNumberVersion = GRADLE_TOOLS_VERSION.substring(0, GRADLE_TOOLS_VERSION.lastIndexOf(".")).toFloat()
    }
    repositories {
        mavenCentral()
        jcenter()
        mavenLocal()
        if (gradleNumberVersion >= 3.4f) {
            google()
        }
    }
    dependencies {
        /**
         * PS: Avoid use "GRADLE_VERSION" because jitpack
         * set this like a environment variable
         */
        print("FCM PLUGIN GRADLE TOOLS VERSION: ${GRADLE_TOOLS_VERSION}")

        classpath "com.android.tools.build:gradle:${GRADLE_TOOLS_VERSION}"

        /**
         * Reference: https://github.com/google/play-services-plugins/issues/22
         *
         * PS: 4.2.0 throws a NullPointerException when use gradle plugin
         *     com.google.gms.googleservices.GoogleServicesPlugin class reference
         *     instead of id(string)
         */

        classpath "com.google.gms:google-services:${GOOGLE_SERVICES_VERSION}"
    }
}
repositories {
    mavenCentral()
    jcenter()
    if (gradleNumberVersion >= 3.4f) {
        google()
    }
}
dependencies {
    if (gradleNumberVersion >= 3.4f) {
        implementation "com.google.firebase:firebase-messaging:${FCM_VERSION}"
    } else {
        compile "com.google.firebase:firebase-messaging:${FCM_VERSION}"
    }
}
// apply plugin: 'com.google.gms.google-services'
// class must be used instead of id(string) to be able to apply plugin from non-root gradle file
apply plugin: com.google.gms.googleservices.GoogleServicesPlugin
